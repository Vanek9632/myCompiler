<program>           ::= <statements>

! ===================================================================
! Statements
! ===================================================================

<statements>      ::= '{' { <statement> } '}'

<statement>       ::= <if_else>
                    | <print>
                    | <input>
                    | <while_loop>
                    | <do_while_loop>
                    | <for_loop>
                    | <init_vars>
                    | <expr_stmt>
                    | ';'

<if_else>         ::= 'if' '(' <expr> ')' <statements>
                    | 'if' '(' <expr> ')' <statements> 'else' <statements>

<print>           ::= 'print' '(' <output_list> ')' ';'
<output_list>     ::= <output> { ',' <output> }
<output>          ::= <string> | <expr>

<input>           ::= 'input' '(' <input_list> ')' ';'
<input_list>      ::= <id> { ',' <id> }

<while_loop>      ::= 'while' '(' <expr> ')' <statements>

<do_while_loop>   ::= 'do' <statements> 'while' '(' <expr> ')' ';'

<for_loop>        ::= 'for' '(' [ <expr> ] ';' [ <expr> ] ';' [ <expr> ] ')' <statements>
                    | 'for' '(' [ <init_vars> ] ';' [ <expr> ] ';' [ <expr> ] ')' <statements>

<init_vars>       ::= <type> <var> { ',' <var> } ';'
<var>             ::= <id>
                    | <id> '=' <expr>
<type>            ::= 'int'

<expr_stmt>       ::= <expr> ';'

! ===================================================================
! Expression
! ===================================================================

<expr>         ::= <assign>

<op_assign>    ::= <id> '='  <op_assign>
                 | <id> '+=' <op_assign>
                 | <id> '-=' <op_assign>
                 | <id> '*=' <op_assign>
                 | <id> '/=' <op_assign>
                 | <id> '%=' <op_assign>
                 | <op_or>

<op_or>        ::= <op_and> '||' <op_and>
                 | <op_and>

<op_and>       ::= <op_equate> '&&' <op_equate>
                 | <op_equate>

<op_equate>    ::= <op_compare> '==' <op_compare>
                 | <op_compare> '!=' <op_compare>
                 | <op_compare>

<op_compare>   ::= <op_add> '<'  <op_add>
                 | <op_add> '>'  <op_add>
                 | <op_add> '<=' <op_add>
                 | <op_add> '>=' <op_add>
                 | <op_add>

<op_add>       ::= <op_mult> '+' <op_mult>
                 | <op_mult> '-' <op_mult>
                 | <op_mult>

<op_mult>      ::= <op_unary> '*' <op_unary>
                 | <op_unary> '/' <op_unary>
                 | <op_unary> '%' <op_unary>
                 | <op_unary>

<op_unary>     ::= '+' <op_unary>
                 | '-' <op_unary>
                 | <factor>

<factor>       ::= <integer>
                 | <id>
                 | '(' <expr> ')'

! ===================================================================
! Characters
! ===================================================================

<string>         ::= '\"' { <all_char> } '\"'

<id>             ::= ( <letter> | '_' ) { <letter> | '_' | <digit> }

<integer>        ::= <digit> <integer>
                   | <digit>

<all_char>       ::= all characters according to ascii table

<assign_char>    ::= "=" | "+=" | "-=" | "*=" | "/=" | "%="

<letter>         ::= "a" | "b" | "c" | "d" | ... | "z"
                   | "A" | "B" | "C" | "D" | ... | "Z"

<digit>          ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" | "0"
